# routeros_system_certificate (Resource)


Certificate resource management consists of two independent processes:
* key creation and certificate signing request (`key` + `csr`)
* certificate signing by the issuer (`crt`)

For a complete certificate creation cycle, both of the above steps must be performed. In this case the `sign {}` block must be specified in the configuration.

If you need to import the current state of the certificate resource, then do not specify the `sign{}` block.

Importing an external certificate is also done without specifying the `sign{}` block, because the certificate should have already been signed by the issuer at this step.

---

## Example Usage
```terraform
resource "routeros_system_certificate" "root_ca" {
  name        = "Test-Root-CA"
  common_name = "RootCA"
  key_usage   = ["key-cert-sign", "crl-sign"]
  trusted     = true
  # Sign Root CA.
  sign {
  }
}

# digitalSignature: Used for entity and data origin authentication with integrity.
# keyEncipherment:  Used to encrypt symmetric key, which is then transferred to target.
# keyAgreement:     Enables use of key agreement to establish symmetric key with target. 

resource "routeros_system_certificate" "server_crt" {
  name        = "Server-Certificate"
  common_name = "server.crt"
  #  KUs: igitalSignature, keyEncipherment or keyAgreement
  key_usage = ["digital-signature", "key-encipherment", "tls-server"]
  sign {
    ca = routeros_system_certificate.root_ca.name
  }
}

resource "routeros_system_certificate" "client_crt" {
  name        = "Client-Certificate"
  common_name = "client.crt"
  key_size    = "prime256v1"
  #  KUs: digitalSignature and/or keyAgreement
  key_usage = ["digital-signature", "key-agreement", "tls-client"]
  sign {
    ca = routeros_system_certificate.root_ca.name
  }
}

resource "routeros_system_certificate" "unsigned_crt" {
  name             = "Unsigned-Certificate"
  common_name      = "unsigned.crt"
  key_size         = "1024"
  subject_alt_name = "DNS:router.lan,DNS:myrouter.lan,IP:192.168.88.1"
}

resource "routeros_system_certificate" "scep_client" {
  name        = "SCEP-Client"
  common_name = "scep-client.crt"
  key_usage   = ["digital-signature", "key-agreement", "tls-client"]

  sign_via_scep {
    scep_url = "http://scep.server/scep/test"
  }
}

#  Import certificate
data "routeros_x509" "cert" {
  data = <<EOT
	-----BEGIN CERTIFICATE-----
	MIIBlTCCATugAwIBAgIINLsws71B5zIwCgYIKoZIzj0EAwIwHzEdMBsGA1UEAwwU
	RXh0ZXJuYWwgQ2VydGlmaWNhdGUwHhcNMjQwNTE3MjEyOTUzWhcNMjUwNTE3MjEy
	OTUzWjAfMR0wGwYDVQQDDBRFeHRlcm5hbCBDZXJ0aWZpY2F0ZTBZMBMGByqGSM49
	AgEGCCqGSM49AwEHA0IABKE1g0Qj4ujIold9tklu2z4BUu/K7xDFF5YmedtOfJyM
	1/80APNboqn71y4m4XNE1JNtQuR2bSZPHVrzODkR16ujYTBfMA8GA1UdEwEB/wQF
	MAMBAf8wDgYDVR0PAQH/BAQDAgG2MB0GA1UdJQQWMBQGCCsGAQUFBwMBBggrBgEF
	BQcDAjAdBgNVHQ4EFgQUNXd5bvluIV9YAhGc5yMHc6OzXpMwCgYIKoZIzj0EAwID
	SAAwRQIhAODte/qS6CE30cvnQpxP/ObWBPIPZnHtkFHIIC1AOSXwAiBGCGQE+aJY
	W72Rw0Y1ckvlt6sU0urkzGuj5wxVF/gSYA==
	-----END CERTIFICATE-----
EOT
}

resource "routeros_file" "key" {
  name = "external.key"
  # The lines of the certificate must not contain indentation.
  contents = <<EOT
-----BEGIN ENCRYPTED PRIVATE KEY-----
MIHeMEkGCSqGSIb3DQEFDTA8MBsGCSqGSIb3DQEFDDAOBAiy/wEW6/MglgICCAAw
HQYJYIZIAWUDBAEqBBD6v8dLA2FjPn62Xz57pcu9BIGQhclivPw1eC2b14ea58Tw
nzDdbYN6/yUiMqapW2xZaT7ZFnbEai4n9/utgtEDnfKHlZvZj2kRhvYoWrvTkt/W
1mkd5d/runsn+B5GO+CMHFHh4t41WMpZysmg+iP8FiiehOQEsWyEZFaedxfYYtSL
Sk+abxJ+NMQoh+S5d73niu1CO8uqQjOd8BoSOurURsOh
-----END ENCRYPTED PRIVATE KEY-----
EOT
}

resource "routeros_file" "cert" {
  name = "external.crt"
  # Normalized certificate
  contents = data.routeros_x509.cert.pem
}

resource "routeros_system_certificate" "external" {
  name        = "external.crt"
  common_name = data.routeros_x509.cert.common_name
  import {
    cert_file_name = routeros_file.cert.name
    key_file_name  = routeros_file.key.name
    passphrase     = "11111111"
  }
  depends_on = [routeros_file.key, routeros_file.cert]
}
```

<!-- schema generated by tfplugindocs -->
## Schema

### Required

- `common_name` (String) Common Name (e.g. server FQDN or YOUR name).
- `name` (String) Name of the certificate. Name can be edited.

### Optional

- `copy_from` (String)
- `country` (String) Country Name (2 letter code).
- `days_valid` (Number) Certificate lifetime.
- `import` (Block Set) (see [below for nested schema](#nestedblock--import))
- `key_size` (String)
- `key_usage` (Set of String) Detailed key usage descriptions can be found in RFC 5280.
- `locality` (String) Locality Name (eg, city).
- `organization` (String) Organizational Unit Name (eg, section)
- `sign` (Block Set) (see [below for nested schema](#nestedblock--sign))
- `sign_via_scep` (Block Set) (see [below for nested schema](#nestedblock--sign_via_scep))
- `state` (String) State or Province Name (full name).
- `subject_alt_name` (String) SANs (subject alternative names).
- `trusted` (Boolean) If set to yes certificate is included 'in trusted certificate chain'.
- `unit` (String) Organizational Unit Name (eg, section).

### Read-Only

- `akid` (String) Authority Key Identifier.
- `authority` (String)
- `ca` (String)
- `ca_crl_host` (String)
- `ca_fingerprint` (String)
- `challenge_password` (String, Sensitive) A challenge password for scep client.
- `crl` (String)
- `digest_algorithm` (Boolean)
- `dsa` (Boolean)
- `expired` (Boolean) Set to true if certificate is expired.
- `expires_after` (String)
- `fingerprint` (String)
- `id` (String) The ID of this resource.
- `invalid_after` (String) The date after which certificate wil be invalid.
- `invalid_before` (String) The date before which certificate is invalid.
- `issued` (String)
- `issuer` (String)
- `key_type` (String)
- `private_key` (Boolean)
- `req_fingerprint` (String)
- `revoked` (String)
- `scep_url` (String)
- `serial_number` (String)
- `skid` (String) Subject Key Identifier.
- `smart_card_key` (String)
- `status` (String) Shows current status of scep client.

<a id="nestedblock--import"></a>
### Nested Schema for `import`

Required:

- `cert_file_name` (String) Certificate file name that will be imported.

Optional:

- `key_file_name` (String) Key file name that will be imported.
- `passphrase` (String, Sensitive) File passphrase if there is such.


<a id="nestedblock--sign"></a>
### Nested Schema for `sign`

Optional:

- `ca` (String) Which CA to use if signing issued certificates.
- `ca_crl_host` (String) CRL host if issuing CA certificate.


<a id="nestedblock--sign_via_scep"></a>
### Nested Schema for `sign_via_scep`

Required:

- `scep_url` (String) HTTP URL to the SCEP server.

Optional:

- `ca_identity` (String) SCEP CA identity.
- `challenge_password` (String, Sensitive) A challenge password.
- `on_smart_card` (Boolean) Whether to store a private key on smart card if hardware supports it.
- `refresh` (Boolean) Check certificate expiration and refresh it if expired.

## Import
Import is supported using the following syntax:
```shell
#The ID can be found via API or the terminal
#The command for the terminal is -> :put [/certificate get [print show-ids]]
#If you plan to manipulate the certificate requiring signing, you need to correctly fill in the sign{} section.
#Changes in the sign{} section will not cause changes in the certificate. It's not a bug, it's a feature!
terraform import routeros_system_certificate.client *9D
```
